plugins {
    id 'java'
    id 'org.jetbrains.intellij.platform' version '2.0.1'
}

def jtregLib = jtregHome + File.separator + "lib" + File.separator + "jtreg.jar"

assert file(jtregLib).exists() : """Could not find jtreg in ${jtregHome}.
Please make sure that 'jtregHome' in 'gradle.properties' points to a valid jtreg root.
"""

version pluginVersion

compileJava {
    sourceCompatibility = javaLevel
    targetCompatibility = javaLevel
}

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    implementation files(jtregLib)

    intellijPlatform {
        intellijIdeaCommunity('2024.2.0.2')
        plugin("AntSupport:${antSupportVersion}")
        bundledPlugins('com.intellij.java', 'TestNG-J')
        instrumentationTools()
    }

}

// See https://github.com/JetBrains/intellij-platform-gradle-plugin/
intellijPlatform {
    pluginConfiguration {
        version = pluginVersion
        ideaVersion {
            sinceBuild = minBuild
            untilBuild = ''
        }
    }
}

patchPluginXml {
    changeNotes = notes
    sinceBuild = minBuild
    untilBuild = ''
}
